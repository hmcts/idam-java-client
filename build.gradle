plugins {
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.springframework.boot' version '3.1.0'
    id 'com.github.ben-manes.versions' version '0.46.0'
    id 'org.owasp.dependencycheck' version '8.2.1'
    id 'java-library'
    id 'maven-publish'
    id 'au.com.dius.pact' version '4.6.0'
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'uk.gov.hmcts.reform'
version buildNumber

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all {
            def isNonStable = { String version ->
                ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                    version ==~ /(?i).*[.-]\$qualifier[.\\d-+]*/
                }
            }
            if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
                selection.reject('Release candidate')
            }
        }
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'

    analyzers {
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
}

checkstyle {
    maxWarnings = 0
    toolVersion = '8.31'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    mavenCentral()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def gitRepo = "https://github.com/hmcts/idam-java-client"

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://www.opensource.org/licenses/MIT"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/idam-java-client"
    }
}

sourceSets {
    test {
        java.srcDir 'src/test/unit/java'
    }
    testContract {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contractTest/resources')
    }
}

configurations {
    testContractCompile.extendsFrom testCompile
    testContractRuntime.extendsFrom testRuntime
}

task contract(type: Test, description: 'Runs the contract tests.', group: 'Verification') {
    useJUnitPlatform()
    testClassesDirs = sourceSets.testContract.output.classesDirs
    classpath = sourceSets.testContract.runtimeClasspath
}

task runAndPublishConsumerPactTests(type: Test){
    useJUnitPlatform()
    testClassesDirs = sourceSets.testContract.output.classesDirs
    classpath = sourceSets.testContract.runtimeClasspath

}

runAndPublishConsumerPactTests.finalizedBy pactPublish

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project.group
            artifactId 'idam-client'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Idam client')
                root.appendNode('name', 'Idam client')
                root.appendNode('url', gitRepo)
                root.children().last() + pomConfig
            }
        }
    }
}

ext {
    lombokVersion = '1.18.28'
    springCloudVersion = '2021.0.4'
    junitJupiterVersion = '5.9.3'
    serenityVersion = '3.5.1'
}

dependencies {
    api group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.0.3'
    api group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    api group: 'io.github.openfeign', name: 'feign-jackson', version: '12.1'
    api group: 'io.github.openfeign', name: 'feign-httpclient', version: '12.3'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'

    testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '4.0.3'

    testContractImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.6.0'

    testContractImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: serenityVersion
    testContractImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: serenityVersion
    testContractImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: serenityVersion
    testContractImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: serenityVersion
    testContractImplementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
    testContractImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testContractImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    testContractRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
    testContractImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion

    testContractImplementation sourceSets.main.runtimeClasspath
    testContractImplementation sourceSets.test.runtimeClasspath
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

test {
    useJUnitPlatform()
}

pact {
    publish {
        pactDirectory = 'build/pacts'
        pactBrokerUrl = System.getProperty("pact.broker.url") ?: 'http://localhost:80'
        tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
}
