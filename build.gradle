plugins {
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'org.owasp.dependencycheck' version '5.3.2.1'
    id 'java-library'
    id 'maven-publish'
    id 'net.ltgt.apt' version '0.21'
    id 'au.com.dius.pact' version '4.1.5'
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'uk.gov.hmcts.reform'
version buildNumber

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all {
            def isNonStable = { String version ->
                ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                    version ==~ /(?i).*[.-]\$qualifier[.\\d-+]*/
                }
            }
            if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
                selection.reject('Release candidate')
            }
        }
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'

    analyzers {
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
}

checkstyle {
    maxWarnings = 0
    toolVersion = '8.31'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

sourceCompatibility = 1.8

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    jcenter()
}

def versions = [
        serentity                       : '2.0.17',
        pact_version                    : '3.5.24',
]

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def gitRepo = "https://github.com/hmcts/idam-java-client"

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/idam-java-client"
    }
}

sourceSets {
    test {
        java.srcDir 'src/test/unit/java'
    }
    testContract {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contractTest/resources')
    }
}

configurations {
    testContractCompile.extendsFrom testCompile
    testContractRuntime.extendsFrom testRuntime
}


task sourcesJar(type: Jar, dependsOn: classes) {
    getArchiveClassifier().set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    getArchiveClassifier().set('javadoc')
    from javadoc.destinationDir
}

task contract(type: Test, description: 'Runs the contract tests.', group: 'Verification') {
    useJUnitPlatform()
    testClassesDirs = sourceSets.testContract.output.classesDirs
    classpath = sourceSets.testContract.runtimeClasspath
}

task runAndPublishConsumerPactTests(type: Test){
    useJUnitPlatform()
    logger.lifecycle("Runs pact Tests")
    testClassesDirs = sourceSets.testContract.output.classesDirs
    classpath = sourceSets.testContract.runtimeClasspath

}

runAndPublishConsumerPactTests.finalizedBy pactPublish

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'idam-client'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Idam client')
                root.appendNode('name', 'Idam client')
                root.appendNode('url', gitRepo)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'idam-client'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = gitRepo
        version {
            name = project.version
        }
    }
}

ext {
    lombokVersion = '1.18.8'
    springCloudVersion = 'Hoxton.SR5'
}

ext["spring-cloud-openfeign.version"] = "2.2.2.RELEASE"

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    api group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
    api group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    api group: 'io.github.openfeign', name: 'feign-jackson', version: '10.9'
    api group: 'io.github.openfeign', name: 'feign-httpclient', version: '10.9'
    api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'

    testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', {
        exclude group: 'com.github.tomakehurst', module: 'wiremock-jre8-standalone'
    }
    testImplementation group: 'com.github.tomakehurst', name:'wiremock-jre8', version: '2.26.3'

    testCompile group: 'au.com.dius', name: 'pact-jvm-provider-spring_2.12', version: versions.pact_version
    testContractCompile group: 'au.com.dius', name: 'pact-jvm-provider-spring_2.12', version: versions.pact_version
    testContractCompile group: 'au.com.dius', name: 'pact-jvm-consumer-junit5_2.12', version: versions.pact_version
    testContractCompile group: 'au.com.dius', name: 'pact-jvm-consumer-java8_2.12', version: versions.pact_version

    testContractCompile group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serentity
    testContractCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serentity
    testContractCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serentity
    testContractCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serentity
    testContractCompile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
    testContractCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testContractCompile("org.junit.jupiter:junit-jupiter-api:5.6.2")
    testContractRuntime("org.junit.jupiter:junit-jupiter-engine:5.6.2")
    testContractImplementation('org.junit.jupiter:junit-jupiter-api:5.3.2')

    testContractCompile sourceSets.main.runtimeClasspath
    testContractCompile sourceSets.test.runtimeClasspath
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

test {
    useJUnitPlatform()
}

pact {
    publish {
        pactDirectory = 'target/pacts'
        pactBrokerUrl = System.getProperty("pact.broker.url") ?: 'http://localhost:80'
        tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}
