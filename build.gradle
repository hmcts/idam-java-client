plugins {
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'java-library'
    id 'maven-publish'
    id 'net.ltgt.apt' version '0.21'
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'uk.gov.hmcts.reform'
version buildNumber

checkstyle {
    maxWarnings = 0
    toolVersion = '8.31'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

sourceCompatibility = 1.8

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    jcenter()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def gitRepo = "https://github.com/hmcts/idam-java-client"

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/idam-java-client"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    getArchiveClassifier().set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    getArchiveClassifier().set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'idam-client'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Idam client')
                root.appendNode('name', 'Idam client')
                root.appendNode('url', gitRepo)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'idam-client'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = gitRepo
        version {
            name = project.version
        }
    }
}

ext {
    lombokVersion = '1.18.8'
    springCloudVersion = 'Hoxton.SR3'
    feignVersion = '10.2.3'
    feignFormVersion = '3.5.0'
    wiremockVersion = '2.24.1'
}

ext["spring-cloud-openfeign.version"] = "2.1.2.RELEASE"

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    api group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
    api group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    api group: 'io.github.openfeign', name: 'feign-jackson', version: feignVersion
    api group: 'io.github.openfeign', name: 'feign-httpclient', version: feignVersion
    api group: 'io.github.openfeign.form', name: 'feign-form', version: feignFormVersion
    api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.1'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.1'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'

    testCompile group: 'com.github.tomakehurst', name:'wiremock-jre8', version: wiremockVersion

    testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

test {
    useJUnitPlatform()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}
